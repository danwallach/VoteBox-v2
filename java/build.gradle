group 'edu.rice.starvote'
version '2.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "com.sparkjava:spark-core:2.5",
            "org.slf4j:slf4j-simple:1.7.21",
            "com.j2html:j2html:0.7",
            "com.pi4j:pi4j-core:1.1",
            fileTree(dir: 'libs', include: '*.jar')

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

buildscript {
    repositories {
        mavenCentral() // or jcenter()
    }
    dependencies {
        classpath 'ch.raffael.pegdown-doclet:pegdown-doclet:1.2'
    }
}
apply plugin: 'ch.raffael.pegdown-doclet'

task swingStartScript(type: CreateStartScripts) {
    mainClassName = 'edu.rice.starvote.ballotbox.SwingController'
    applicationName = 'ballotboxsw'
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
}

distributions {
    main {
        contents {
            from("../../STAR-Vote/keys") {
                into "keys"
            }
            from ("../../STAR-Vote/rules") {
                into "rules"
            }
            from(swingStartScript) {
                into "bin"
                fileMode = 0755
            }
        }
    }
}

mainClassName = 'edu.rice.starvote.ballotbox.STARController'

